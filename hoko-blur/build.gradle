apply plugin: 'com.android.library'

def isReleaseBuildType = false
gradle.startParameter.taskNames.each({
    String taskNameL = it.toLowerCase()
    if (taskNameL.contains("release")) {
        isReleaseBuildType = true
    }
    if (taskNameL.equalsIgnoreCase('uploadArchives')) {
        isReleaseBuildType = true
    }
})

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-19', '-DANDROID_TOOLCHAIN=clang'
                cppFlags "-std=c++11 -frtti -fexceptions"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            jniDebuggable true
        }
    }

    lintOptions {
        abortOnError false
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.annotation:annotation:1.1.0'
}

ext {
    bintrayRepo = 'Hoko'
    bintrayName = 'HokoBlur'

    publishedGroupId = 'com.hoko'
    libraryName = 'HokoBlur'
    artifact = 'hoko-blur'

    libraryDescription = 'a blur effect generator library'

    siteUrl = 'https://github.com/HokoFly/HokoBlur'
    gitUrl = 'https://github.com/HokoFly/HokoBlur.git'

    libraryVersion = HOKO_BLUR_VERSION

    developerId = 'HokoFly'
    developerName = 'HokoFly'
    developerEmail = 'yuxfzju@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply from: rootProject.file('gradle/bintray.gradle')